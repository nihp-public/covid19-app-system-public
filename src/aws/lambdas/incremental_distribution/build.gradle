import java.text.DecimalFormat

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath Libs.org_jetbrains_kotlin_jvm_gradle_plugin
        classpath Libs.com_adarshr_test_logger_gradle_plugin
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.adarshr.test-logger'

    group = 'com.demo'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            name "amazonaws:DynamoDBLocal Release Repo"
            url "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release"
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    compileKotlin.kotlinOptions {
        jvmTarget = "11"
        suppressWarnings = false
        freeCompilerArgs += "-Xjvm-default=all"
    }

    compileTestKotlin.kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-Xjvm-default=all"
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs += ["-parameters"]
    }

    testlogger {
        theme 'standard-parallel'
    }

    task zipLambdaDistribution(type: Zip) {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        destinationDirectory = new File(rootDir, "../../../../out/build")
        from compileJava
        from compileKotlin
        from processResources
        into("lib") {
            from configurations.runtimeClasspath
        }
    }

    task lambdaZip {
        dependsOn(check, zipLambdaDistribution)
    }

    test {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        useJUnitPlatform()

        testlogger {
            showPassed false
            showSkipped false
            slowThreshold 5000
        }

        reports.html.destination = file("$projectDir/../../../../../out/reports/testUnit_${project.name}")
        reports.junitXml.destination = file("$projectDir/../../../../../out/reports/testUnit_${project.name}")
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    artifacts {
        testArtifacts testJar
    }

    dependencies {
        testApi(platform(Libs.junit_bom))
        testApi(Libs.junit_jupiter)
        testApi(Libs.junit_jupiter_params)

        testApi(platform(Libs.http4k_bom))
        testApi(Libs.http4k_format_moshi)
        testApi(Libs.http4k_testing_approval)
        testApi(Libs.http4k_testing_servirtium)
        testApi(Libs.http4k_testing_strikt)

        testApi(Libs.strikt_jvm)
        testApi(Libs.jsonassert)
        testApi(Libs.javafaker)

        testApi(Libs.mockk)
        testApi(Libs.awaitility)
        testApi(Libs.snodge)
        testApi(Libs.jose4j)

        testApi(Libs.wiremock_jre8)
        testApi(Libs.bcpkix_jdk15on)
        testApi(Libs.bcprov_ext_jdk15on)

        testRuntimeOnly(Libs.javax_json)
    }
}

tasks.create("weighDependencies") {
    doLast {
        allprojects
            .forEach {
                println it.name.toUpperCase()
                it.configurations
                    .runtimeClasspath
                    .resolvedConfiguration
                    .firstLevelModuleDependencies.forEach {
                    def sorted = it.allModuleArtifacts.toSorted()
                    def sum = sorted.sum { it.file.length() }

                    println "  " + it.name + " = " + (sum ? new DecimalFormat("#.00Mb").format(sum / 1024d / 1000d) : "0Mb")
                    sorted.forEach { println("    " + it.file.name + " = " + it.file.length()) }
                }
            }
    }
}
